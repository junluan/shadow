set(shadow_cpu_src)
set(shadow_gpu_src)
set(shadow_examples_src)
set(shadow_server_src)
set(shadow_client_src)
set(shadow_test_src)
set(shadow_tools_src)

add_subdirectory(core)
add_subdirectory(examples)
add_subdirectory(operators)
add_subdirectory(proto)
add_subdirectory(server)
add_subdirectory(tools)
add_subdirectory(util)

if (${BUILD_TEST})
  add_subdirectory(test)
endif ()

include_directories(".")

if (${USE_CUDA} AND CUDA_FOUND)
  cuda_add_library(shadow ${shadow_cpu_src} ${shadow_gpu_src})
else ()
  add_library(shadow ${shadow_cpu_src})
endif ()
target_link_libraries(shadow ${Shadow_LINKER_LIBS})
if (${USE_Protobuf} AND Protobuf_FOUND)
  target_link_libraries(shadow ${Shadow_PROTO_LIB} ${Protobuf_LIBRARIES})
endif ()
if (MSVC)
  set_target_properties(shadow PROPERTIES DEBUG_POSTFIX "d")
endif ()
install(TARGETS shadow DESTINATION ${Shadow_INSTALL_LIB_PREFIX})
install(DIRECTORY core operators util DESTINATION ${Shadow_INSTALL_INCLUDE_PREFIX} FILES_MATCHING PATTERN "*.hpp" PATTERN "*.h")
if (${USE_Protobuf} AND Protobuf_FOUND)
  install(DIRECTORY proto DESTINATION ${Shadow_INSTALL_INCLUDE_PREFIX} FILES_MATCHING PATTERN "*.hpp" PATTERN "*.h")
endif ()

if (BUILD_SHARED_LIBS)
  set(Shadow_LIB shadow)
else ()
  add_whole_archive_flag(shadow Shadow_LIB)
endif ()

if (${BUILD_EXAMPLES})
  add_executable(test_demo "examples/test_demo.cpp" ${shadow_examples_src})
  target_link_libraries(test_demo ${Shadow_LIB})
  install(TARGETS test_demo DESTINATION ${Shadow_INSTALL_BIN_PREFIX})
endif ()

if (${BUILD_TOOLS} AND Protobuf_FOUND)
  add_executable(convert ${shadow_tools_src})
  target_compile_definitions(convert PRIVATE -DUSE_Protobuf)
  target_link_libraries(convert ${Shadow_PROTO_LIB} ${Protobuf_LIBRARIES})
  install(TARGETS convert DESTINATION ${Shadow_INSTALL_BIN_PREFIX})
endif ()

if (${BUILD_SERVICE} AND Protobuf_FOUND AND gRPC_FOUND)
  foreach (server_src ${shadow_server_src})
    get_filename_component(server_name ${server_src} NAME_WE)
    add_executable(${server_name} ${server_src} ${shadow_examples_src})
    target_link_libraries(${server_name} ${Shadow_LIB} ${Shadow_GRPC_PROTO_LIB} ${gRPC_LIBRARIES} ${Protobuf_LIBRARIES})
    target_link_libraries(${server_name} ${CMAKE_THREAD_LIBS_INIT} ${ZLIB_LIBRARIES})
    install(TARGETS ${server_name} DESTINATION ${Shadow_INSTALL_BIN_PREFIX})
  endforeach ()

  foreach (client_src ${shadow_client_src})
    get_filename_component(client_name ${client_src} NAME_WE)
    add_executable(${client_name} ${client_src})
    target_link_libraries(${client_name} ${Shadow_GRPC_PROTO_LIB} ${gRPC_LIBRARIES} ${Protobuf_LIBRARIES})
    target_link_libraries(${client_name} ${CMAKE_THREAD_LIBS_INIT} ${ZLIB_LIBRARIES})
    install(TARGETS ${client_name} DESTINATION ${Shadow_INSTALL_BIN_PREFIX})
  endforeach ()
endif ()

if (${BUILD_TEST})
  add_executable(test_shadow ${shadow_test_src})
  target_link_libraries(test_shadow ${Shadow_LIB} ${GoogleTest_LIBRARIES})
  install(TARGETS test_shadow DESTINATION ${Shadow_INSTALL_BIN_PREFIX})
endif ()
